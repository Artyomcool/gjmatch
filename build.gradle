plugins {
    id 'groovy'
    id 'java'
    id 'maven'
    id 'signing'
    id 'jacoco'
}

group = 'com.github.artyomcool'
archivesBaseName = 'gjmatch'
version = '0.1'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.codehaus.groovy:groovy-all:3.0.3'

    testImplementation 'org.codehaus.groovy:groovy-all:3.0.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.4.2'
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty('nexusUsername')) {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }
            }

            pom.project {
                name 'GJMatch'
                packaging 'jar'
                description 'Library for testing and verifying JSON using groovy GString syntax.'
                url 'https://github.com/Artyomcool/gjmatch'

                scm {
                    connection 'scm:git:ssh://github.com/Artyomcool/gjmatch'
                    developerConnection 'scm:git:ssh://github.com/Artyomcool/gjmatch'
                    url 'https://github.com/Artyomcool/gjmatch'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'artyomcool'
                        name 'Artyom Drozdov'
                        email 'artyomcool2@gmail.com'
                    }
                }
            }
        }
    }
}